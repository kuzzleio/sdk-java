import org.gradle.api.JavaVersion

plugins {
    id "com.jfrog.bintray" version "1.8.5"
    id 'jacoco'
    id 'java'
}

allprojects {
    repositories {
        jcenter()
    }
    apply plugin: 'idea'
    apply plugin: 'maven-publish'
    apply plugin: 'java'
    apply plugin: 'java-library'
}

group = 'io.kuzzle'
version = '3.0.0'

sourceCompatibility = 1.8

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['MyPublication']

    pkg {
        repo = 'maven'
        name = 'kuzzle-sdk-java'
        userOrg = 'kuzzle'
        licenses = ['Apache-2.0']
        desc = 'Kuzzle JAVA SDK'
        vcsUrl = 'https://github.com/kuzzleio/sdk-java.git'
        websiteUrl = 'https://kuzzle.io'
        issueTrackerUrl = 'https://github.com/kuzzleio/sdk-java/issues'
        publicDownloadNumbers = true
        publish = true
    }
}

repositories {
    jcenter()
}

def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "kuzzle"
            name "kuzzle"
            email "support@kuzzle.io"
        }
    }

    scm {
        url "https://github.com/kuzzleio/sdk-java"
    }
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'io.kuzzle'
            artifactId 'kuzzle-sdk-java'
            version version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Kuzzle JAVA SDK')
                root.appendNode('name', 'kuzzle-sdk-java')
                root.appendNode('url', 'https://github.com/kuzzleio/sdk-java')
                root.children().last() + pomConfig
            }
        }
    }
}

dependencies {
    implementation 'com.neovisionaries:nv-websocket-client:2.9'
    implementation 'com.google.code.gson:gson:2.8.5'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testImplementation 'org.mockito:mockito-core:1.9.5'
    testImplementation 'org.skyscreamer:jsonassert:1.5.0'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

check.dependsOn jacocoTestReport

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

test {
    useJUnit()

    maxHeapSize = '1G'
}

